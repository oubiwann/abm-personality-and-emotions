;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;   Agent Support Procedures   ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to-report member-personality? [agent personality-ids]
  let agent-personality [personality-id] of agent
  if member? agent-personality personality-ids [ report true ]
  report false
end

to-report agent-intersection [agents personality-ids]
  if agents = nobody [ report agents ]
  if personality-ids = [] [ report nobody ]
  report turtle-set (map [x -> agents with [personality-id = x]] personality-ids)
end

to-report filter-attracted-agents [agent]
  report agent-intersection [personality-neighbors] of agent
                            (attracted-lookup [personality-id] of agent)
end

to-report filter-repulsed-agents [agent]
  report agent-intersection [personality-neighbors] of agent
                            (repulsed-lookup [personality-id] of agent)
end

to-report attracted-agent-distances [agent]
  report agent-distances agent (filter-attracted-agents agent)
end

to-report repulsed-agent-distances [agent]
  report agent-distances agent (filter-repulsed-agents agent)
end

to-report attracted-agent-coords [agent]
  report agents-coords (filter-attracted-agents agent)
end

to-report repulsed-agent-coords [agent]
  report agents-coords (filter-repulsed-agents agent)
end

to-report attracted-center-of-charge [agent]
  let agents (filter-attracted-agents agent)
  if agents = nobody or not any? agents [
    report agent-coords agent
  ]
  report center-of-charge (agent-augmented-coords
                           agent
                           agents)
end

to-report repulsed-center-of-charge [agent]
  let agents (filter-repulsed-agents agent)
  if agents = nobody or not any? agents [
    report agent-coords agent
  ]
  report center-of-charge (agent-augmented-coords
                           agent
                           agents)
end

to-report summed-center-of-charge [agent]
  let attracted-coords attracted-center-of-charge agent
  let repulsed-coords repulsed-center-of-charge agent
  let adj-repulsed-coords flip-point repulsed-coords agent-coords agent
  report center-of-charge (list attracted-coords adj-repulsed-coords)
end
